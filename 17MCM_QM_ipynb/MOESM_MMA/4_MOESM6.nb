(* Content-type: application/vnd.wolfram.mathematica *)

(*** Wolfram Notebook File ***)
(* http://www.wolfram.com/nb *)

(* CreatedBy='Mathematica 10.4' *)

(*CacheID: 234*)
(* Internal cache information:
NotebookFileLineBreakTest
NotebookFileLineBreakTest
NotebookDataPosition[       158,          7]
NotebookDataLength[     34318,        894]
NotebookOptionsPosition[     29670,        824]
NotebookOutlinePosition[     30023,        840]
CellTagsIndexPosition[     29980,        837]
WindowFrame->Normal*)

(* Beginning of Notebook Content *)
Notebook[{
Cell[TextData[{
 "This Mathematica code is part of the lecture script ",
 StyleBox["Introduction to Computational Quantum Mechanics",
  FontSlant->"Italic"],
 " by Roman Schmied. It is licensed under a ",
 ButtonBox["Creative Commons Attribution-ShareAlike 4.0 International License",
  BaseStyle->"Hyperlink",
  ButtonData->{
    URL["http://creativecommons.org/licenses/by-sa/4.0/"], None},
  ButtonNote->"http://creativecommons.org/licenses/by-sa/4.0/"],
 "."
}], "Text",
 CellChangeTimes->{{3.669434091682116*^9, 3.669434147842166*^9}, {
  3.669435842013185*^9, 3.669435866952169*^9}, {3.669435915352921*^9, 
  3.669435921977655*^9}}],

Cell[BoxData[
 GraphicsBox[
  TagBox[RasterBox[CompressedData["
1:eJztWltPXFUUJuqDMSnhnYdiJCmYYKimXl5aTH3xDWtMfKlCFRuThhJobEgw
IqGaRiMhGKwmgE3f5DLcBpgLzMDMMHdmhpa+0oSH+kb4Bcv97cM63edw5nLm
UlrsShac2Ze1z/n22t9ae5/z+pXrl755qaqqqudV8edS+80Pu7vbez+tET8+
6+z59mpnx9cfd97ouNrR/f6Vl0XhaaGfC31FKBFV498LrYhW8bXTvUjL3iVy
ra2Qd91Da4E1Wg/5KRAJ0GYsRJFEmKLJKMW3YpRIxSmRTmgqruOpGMVEHdqg
bVD0QV+fsAFbrjWXtI0xFlbmaW55lmadDppZnKbphSmpU/OTNDn3z4lRFWfG
1u1z0eqGl/xBHwXCGxKraDIi8dvKJCl9P0WZB2na3tmm+w81xXXmQUbWoU1c
4I0+6Asb/pBP2oRtM8YO54yOMfA9qRgbsBV+B/8Lx8PSV4EZMB28NUgXP7pI
p6pPHVkHKEPd4E+Dsu3W9pacE/hzMBqUvry6sWrAeH5ljmaXHBrGC9N5fbi3
r5caGxuPjI0y1JWKRSXssw1wgootcEmk49Inh0eGqba2tmDOQdvh34dlX/AH
fDmkY+zVuWLRtUDzyxrGuXx45M4INTQ26PZbWlqoq6tLKq65HG3Q1i4GlbTP
fcGR4IQn2CakH7Z+0mrArqamhtra2qi/v598Pp9UXKMMdWpb9A2GA5TMaBjD
j8EVGh+v0JLHaeQJCx+euDdO1dXVmr3WVtrd3SWzoAx1aIO2djCotH3GArEM
XAlOgN+asQV2wHF/f//I+KpMTEwYcMacA2PMF+YNYyDmefxuWlld1njCwof5
/tivMH+qsH3z2Dxmoc9vZX9vb48i4bBUXJdin+8TaxfxCHyLda1i29zcbDmv
2QRzgD6qH8Mm+BhjPOEJxYeXhA8vzhg4ApzHfvVwZycvvhD2s0L40sr+vbt3
6Y3TdQZ1u1xF2VfxDUheiMhYBr5Vsc3ns4VgDD5GzJM8IcbyBTUfljzs1niY
OYLx5VhjZ27RlmNSvmc324e/As8L731AX33ZTt/13NAxHh4asm1fxVf6rvAv
8ALHMqzzVCplG1sVY+YK2IRt5G7sw951zYfVXELlCI41ZsFa5vs28waEY1Ih
z872Dw4OaGL0L4nlbz//otsaHxuTZWeb3rJtX8UX3JgUvoscjMvAt6UKcxYU
uRvWB/Pw2mG+pnKEGV/EcLOY8xWzoE+h+LJ94FtfXy+xbGho0G2Bf9mH/338
2JZ9FV/sy1KCI5HDsu+qvIA8AdyDuYOq2Jvrhg7XEgv7MGyDh7HPO8IRIleT
eYTCwU8TX4jT6aSmpib5PFb4Ptp9VDS+iGtYv7x3UNcdxrPKc4Glw+GwrFP7
83qGbezzwEOhw1zN4/fIPAIczHnacfADBHEM8S0aiehlU5OTOr527av44gxh
eyej/1Z9kONUXV2d5HfgjWusfa7jHAN4cx2LyhHYSycMHOwx5GkqvrniWzbf
LSW+MddCB38ckPrmmTPy953RP2zbN+Arcl6cJfBvdY3k4uNCuFr1f4yBXHgz
tpkXXzV/KhTfYvMzFmBqzs86r12T/GzX/rOOL+7Pzv6CebHU/UU8Fpe8AFXz
7mLs2+UHxDzkbLgGpio/mOtYiuUH3N9J2R/nim+5Ylgh8Y3nXYtvaTlWIfFN
xUA9f8Fcwj7sqnuYYs93KmnfnJ/xvhg5lXmNZ8vPgDGXm+sg8Gc1P4sq+Znb
lz0/s+LL5/V8kvcX6t7YnMcWI9n2FxuH+wucVfL+ItsZz/Os/OyhqPX+2M7e
3yyl7o+PG5ty4que74z/PWbgorKc74h1Ads438FYfEapnrObz3eOG5ty4gvt
+6FPP5+8/MVlA8Z2znng85bnk8L2wK0By3j4f9Gu7usyf0oXcb6OOrRRz9fP
vXtO8gLya8zfcT/fs6DfCxywBwDGqh8zznbeDwHbpLA1+udoSfekilpmblOJ
MUq1mxXjlPZ+E3xs+/3mCL/fjJeMrRk7q+tyYFvomOVScIX6fh6Y5Xs/j/cU
8v08voMQfcvFCVa+Ve5nz+a7lfJh6NvvnCXHvEPmbsiPU/r3JRnl+5KMLFO/
L5ldcND5C+fLdh+V5IVsY1Xaf1UFVrd/vU1ev1f5PupQ8X2UKEMd2pQTVytf
yoV9Oe0/TXxfaElz99px38MJ1qr/AASW71I=
    "], {{0, 31}, {88, 0}}, {0, 255},
    ColorFunction->RGBColor],
   BoxForm`ImageTag["Byte", ColorSpace -> "RGB", Interleaving -> True],
   Selectable->False],
  DefaultBaseStyle->"ImageGraphics",
  ImageSize->Automatic,
  ImageSizeRaw->{88, 31},
  PlotRange->{{0, 88}, {0, 31}}]], "Text",
 CellChangeTimes->{3.669607172458665*^9}],

Cell[TextData[{
 "You can download the associated lecture script at ",
 ButtonBox["http://arxiv.org/abs/1403.7050",
  BaseStyle->"Hyperlink",
  ButtonData->{
    URL["http://arxiv.org/abs/1403.7050"], None},
  ButtonNote->"http://arxiv.org/abs/1403.7050"]
}], "Text",
 CellChangeTimes->{{3.669434151914299*^9, 3.669434159778274*^9}, {
  3.669435746836981*^9, 3.669435760656741*^9}, {3.669435890511055*^9, 
  3.6694358923333282`*^9}, {3.682242719652669*^9, 3.6822427205773983`*^9}}],

Cell[CellGroupData[{

Cell["Bose-Einstein condensate in a 3D harmonic trap", "Title",
 CellChangeTimes->{{3.669356489273039*^9, 3.669356492332947*^9}, {
  3.672571380907549*^9, 3.6725713873462048`*^9}, {3.682678290678982*^9, 
  3.68267833186395*^9}, {3.682764418521792*^9, 3.682764435924129*^9}, {
  3.682913374723269*^9, 3.682913383085833*^9}, {3.683439269427219*^9, 
  3.6834392787068233`*^9}}],

Cell[CellGroupData[{

Cell["units", "Subsection",
 CellChangeTimes->{{3.737523028929317*^9, 
  3.73752302941407*^9}},ExpressionUUID->"5b40b627-07a8-4c50-8afd-\
7e1243a2c4e4"],

Cell["consistent units:", "Text",
 CellChangeTimes->{{3.737523346886271*^9, 3.737523348350995*^9}, {
  3.737547135477292*^9, 
  3.737547142345888*^9}},ExpressionUUID->"7720af8b-52db-4ea6-b828-\
7009757ca7a9"],

Cell[BoxData[{
 RowBox[{
  RowBox[{"LengthUnit", "=", 
   RowBox[{"Quantity", "[", "\"\<Micrometers\>\"", "]"}]}], 
  ";"}], "\[IndentingNewLine]", 
 RowBox[{
  RowBox[{"TimeUnit", "=", 
   RowBox[{"Quantity", "[", "\"\<Seconds\>\"", "]"}]}], 
  ";"}], "\[IndentingNewLine]", 
 RowBox[{
  RowBox[{"MassUnit", "=", 
   FractionBox[
    RowBox[{
     RowBox[{"Quantity", "[", "\"\<ReducedPlanckConstant\>\"", "]"}], 
     "TimeUnit"}], 
    SuperscriptBox["LengthUnit", "2"]]}], ";"}], "\[IndentingNewLine]", 
 RowBox[{
  RowBox[{"EnergyUnit", "=", 
   FractionBox[
    RowBox[{"Quantity", "[", "\"\<ReducedPlanckConstant\>\"", "]"}], 
    "TimeUnit"]}], ";"}]}], "Input",
 CellChangeTimes->{{3.737523056034811*^9, 3.737523160551105*^9}, 
   3.737523343368023*^9, {3.73754714816612*^9, 3.737547183099942*^9}, {
   3.751372050002721*^9, 3.751372050246688*^9}},
 CellLabel->"In[1]:=",ExpressionUUID->"4c9b1889-e5b5-4da7-9b3d-25e836fbd2ea"],

Cell["physical constants:", "Text",
 CellChangeTimes->{{3.737523354617507*^9, 
  3.737523360488123*^9}},ExpressionUUID->"026f9324-bb7b-4b4d-b673-\
2692fcf11544"],

Cell[BoxData[
 RowBox[{
  RowBox[{"\[HBar]", "=", 
   FractionBox[
    RowBox[{"Quantity", "[", "\"\<ReducedPlanckConstant\>\"", "]"}], 
    RowBox[{"EnergyUnit", "*", "TimeUnit"}]]}], ";"}]], "Input",
 CellChangeTimes->{{3.737523248301085*^9, 3.7375232623640747`*^9}, 
   3.737523340468092*^9},
 CellLabel->"In[5]:=",ExpressionUUID->"366fc385-13d4-41f7-9699-bb04076279b6"]
}, Open  ]],

Cell[CellGroupData[{

Cell["system parameters", "Subsection",
 CellChangeTimes->{{3.683439722578678*^9, 
  3.683439728259124*^9}},ExpressionUUID->"dec91d92-e1c9-4e07-add9-\
c889f4001151"],

Cell["particle mass:", "Text",
 CellChangeTimes->{{3.7375232886347637`*^9, 
  3.737523292601243*^9}},ExpressionUUID->"440f7527-ac18-4180-8626-\
961743152a32"],

Cell[BoxData[
 RowBox[{
  RowBox[{"m", "=", 
   FractionBox[
    RowBox[{"Quantity", "[", 
     RowBox[{"86.909187", ",", "\"\<AtomicMassUnit\>\""}], "]"}], 
    "MassUnit"]}], ";"}]], "Input",
 CellChangeTimes->{{3.7375231482813377`*^9, 3.7375231643428164`*^9}, 
   3.737523293920553*^9},
 CellLabel->"In[6]:=",ExpressionUUID->"f36d9a86-0548-40aa-807d-f1983d8a9a17"],

Cell["size of the simulation box:", "Text",
 CellChangeTimes->{{3.737523295966914*^9, 
  3.7375232994639893`*^9}},ExpressionUUID->"15a5199b-6353-4840-86c6-\
f04d25c49129"],

Cell[BoxData[
 RowBox[{
  RowBox[{"a", "=", 
   FractionBox[
    RowBox[{"Quantity", "[", 
     RowBox[{"10", ",", "\"\<Micrometers\>\""}], "]"}], "LengthUnit"]}], 
  ";"}]], "Input",
 CellChangeTimes->{{3.7375231691313*^9, 3.73752317604692*^9}, 
   3.737523301191761*^9},
 CellLabel->"In[7]:=",ExpressionUUID->"1675341c-34a2-439d-9494-632eca5b9b69"],

Cell["harmonic trap frequencies:", "Text",
 CellChangeTimes->{{3.737523303545981*^9, 
  3.737523310211515*^9}},ExpressionUUID->"ec4273ad-8611-4671-a29f-\
ca5bb5800eeb"],

Cell[BoxData[{
 RowBox[{
  RowBox[{"\[Omega]x", "=", 
   RowBox[{"2", "\[Pi]", "*", 
    RowBox[{"Quantity", "[", 
     RowBox[{"115", ",", "\"\<Hertz\>\""}], "]"}], "*", "TimeUnit"}]}], 
  ";"}], "\n", 
 RowBox[{
  RowBox[{"\[Omega]y", "=", 
   RowBox[{"2", "\[Pi]", "*", 
    RowBox[{"Quantity", "[", 
     RowBox[{"540", ",", "\"\<Hertz\>\""}], "]"}], "*", "TimeUnit"}]}], 
  ";"}], "\n", 
 RowBox[{
  RowBox[{"\[Omega]z", "=", 
   RowBox[{"2", "\[Pi]", "*", 
    RowBox[{"Quantity", "[", 
     RowBox[{"540", ",", "\"\<Hertz\>\""}], "]"}], "*", "TimeUnit"}]}], 
  ";"}]}], "Input",
 CellChangeTimes->{{3.737523179482963*^9, 3.7375231913351183`*^9}, {
  3.737523315154903*^9, 3.737523319804311*^9}},
 CellLabel->"In[8]:=",ExpressionUUID->"50cfe317-8e41-4799-a20b-8c2f66a25a3f"],

Cell["s-wave scattering cross section:", "Text",
 CellChangeTimes->{{3.7375233240838346`*^9, 
  3.737523332328863*^9}},ExpressionUUID->"1e7ddf49-ccaa-4e52-8643-\
66c8ffae7ec9"],

Cell[BoxData[
 RowBox[{
  RowBox[{"as", "=", 
   FractionBox[
    RowBox[{"Quantity", "[", 
     RowBox[{"100.4", ",", "\"\<BohrRadius\>\""}], "]"}], "LengthUnit"]}], 
  ";"}]], "Input",
 CellChangeTimes->{{3.737523215040845*^9, 3.7375232425588493`*^9}, 
   3.7375233335631123`*^9},
 CellLabel->"In[11]:=",ExpressionUUID->"c4039ab4-7cfb-40b2-b242-bbfc31ee7c14"],

Cell[TextData[{
 "effective interaction strength: ",
 Cell[BoxData[
  FormBox[
   RowBox[{
    RowBox[{"(", 
     RowBox[{"energy", " ", "unit"}], ")"}], "\[Times]", 
    SuperscriptBox[
     RowBox[{"(", 
      RowBox[{"length", " ", "unit"}], ")"}], "3"]}], TraditionalForm]],
  FormatType->"TraditionalForm",ExpressionUUID->
  "08939d5c-a044-45c5-aa14-0caed8bcae6e"]
}], "Text",
 CellChangeTimes->{{3.737537302509489*^9, 3.73753730883851*^9}, {
  3.737537384324679*^9, 3.73753738644871*^9}, {3.73753751983395*^9, 
  3.7375375393619823`*^9}},ExpressionUUID->"820b83c9-4197-4cb2-beae-\
08f7944d7729"],

Cell[BoxData[
 RowBox[{
  RowBox[{"\[Kappa]", "=", 
   FractionBox[
    RowBox[{"4", "\[Pi]", " ", 
     SuperscriptBox["\[HBar]", "2"], "as"}], "m"]}], ";"}]], "Input",
 CellChangeTimes->{{3.737537310270269*^9, 3.7375373108103952`*^9}, {
  3.737537370460651*^9, 3.737537380074251*^9}},
 CellLabel->"In[12]:=",ExpressionUUID->"0dd9b70a-a4f8-4e2f-899a-6a4edfc5c218"]
}, Open  ]],

Cell[CellGroupData[{

Cell["grid setup", "Subsection",
 CellChangeTimes->{{3.683439731060783*^9, 
  3.683439733018796*^9}},ExpressionUUID->"38882b32-ffb2-4aa5-919d-\
2f996d7820e7"],

Cell[TextData[{
 "the grid spans the interval ",
 Cell[BoxData[
  FormBox[
   RowBox[{"[", 
    RowBox[{
     RowBox[{"-", 
      FractionBox["a", "2"]}], ",", 
     FractionBox["a", "2"]}], "]"}], TraditionalForm]],ExpressionUUID->
  "5b201e31-c02d-4dee-a141-2ffe89e347d8"],
 " in each direction:"
}], "Text",
 CellChangeTimes->{{3.6834397531901093`*^9, 3.683439769243441*^9}, {
  3.7375233744192247`*^9, 
  3.7375233757038393`*^9}},ExpressionUUID->"b0d6fb20-bb17-437a-bca0-\
aa321f414a83"],

Cell[BoxData[{
 RowBox[{
  RowBox[{"nmax", "=", "50"}], ";"}], "\n", 
 RowBox[{
  RowBox[{"\[CapitalDelta]", "=", 
   FractionBox["a", 
    RowBox[{"nmax", "+", "1"}]]}], ";"}], "\[IndentingNewLine]", 
 RowBox[{
  RowBox[{"xgrid", "=", 
   RowBox[{"a", 
    RowBox[{"(", 
     RowBox[{
      FractionBox[
       RowBox[{"Range", "[", "nmax", "]"}], 
       RowBox[{"nmax", "+", "1"}]], "-", 
      FractionBox["1", "2"]}], ")"}]}]}], ";"}]}], "Input",
 CellChangeTimes->{{3.683439733906474*^9, 3.6834397480444803`*^9}, {
   3.684062641485093*^9, 3.68406264183412*^9}, {3.68406307658187*^9, 
   3.684063076930249*^9}, {3.7375233807571487`*^9, 3.737523396319745*^9}, 
   3.737523686782744*^9, 3.7513729997570333`*^9},
 CellLabel->"In[13]:=",ExpressionUUID->"d5e165f9-d0ea-431b-97e9-c0ea487b131a"],

Cell["the harmonic trap potential:", "Text",
 CellChangeTimes->{{3.6840572024000273`*^9, 
  3.684057208868475*^9}},ExpressionUUID->"7b1e58aa-6e2a-4411-ae34-\
630fa08a2713"],

Cell[BoxData[
 RowBox[{
  RowBox[{
   RowBox[{"W", "[", 
    RowBox[{"x_", ",", "y_", ",", "z_"}], "]"}], "=", 
   RowBox[{
    RowBox[{
     FractionBox["1", "2"], "m", 
     RowBox[{"(", 
      RowBox[{
       RowBox[{
        SuperscriptBox["\[Omega]x", "2"], 
        SuperscriptBox["x", "2"]}], "+", 
       RowBox[{
        SuperscriptBox["\[Omega]y", "2"], 
        SuperscriptBox["y", "2"]}], "+", 
       RowBox[{
        SuperscriptBox["\[Omega]z", "2"], 
        SuperscriptBox["z", "2"]}]}], ")"}]}], "//", "Expand"}]}], 
  ";"}]], "Input",
 CellChangeTimes->{{3.684057162663692*^9, 3.684057195910097*^9}, {
  3.7375236969983587`*^9, 3.737523718930872*^9}},
 CellLabel->"In[16]:=",ExpressionUUID->"bdba2384-b042-429f-9785-5fb895425dc6"],

Cell["evaluate this potential on the computational grid in 3D:", "Text",
 CellChangeTimes->{{3.684057211973111*^9, 
  3.684057221603544*^9}},ExpressionUUID->"01f8f462-2e4a-4f24-800f-\
f60397a70103"],

Cell[BoxData[
 RowBox[{
  RowBox[{"Wgrid", "=", 
   RowBox[{"Outer", "[", 
    RowBox[{"W", ",", "xgrid", ",", "xgrid", ",", "xgrid"}], "]"}]}], 
  ";"}]], "Input",
 CellChangeTimes->{{3.6840572306002207`*^9, 3.68405723260637*^9}, {
  3.751373002990308*^9, 3.7513730044568443`*^9}},
 CellLabel->"In[17]:=",ExpressionUUID->"bff51171-fecc-4661-8533-694db4c77dd5"],

Cell["position operators:", "Text",
 CellChangeTimes->{{3.6840619200118628`*^9, 
  3.684061923520645*^9}},ExpressionUUID->"9b9f12a4-abc3-4b0d-a022-\
4fd31dbe7e8e"],

Cell[BoxData[{
 RowBox[{
  RowBox[{"ones", "=", 
   RowBox[{"ConstantArray", "[", 
    RowBox[{"1", ",", "nmax"}], "]"}]}], ";"}], "\[IndentingNewLine]", 
 RowBox[{
  RowBox[{"xc", "=", 
   RowBox[{"Outer", "[", 
    RowBox[{"Times", ",", "xgrid", ",", "ones", ",", "ones"}], "]"}]}], 
  ";"}], "\[IndentingNewLine]", 
 RowBox[{
  RowBox[{"yc", "=", 
   RowBox[{"Outer", "[", 
    RowBox[{"Times", ",", "ones", ",", "xgrid", ",", "ones"}], "]"}]}], 
  ";"}], "\[IndentingNewLine]", 
 RowBox[{
  RowBox[{"zc", "=", 
   RowBox[{"Outer", "[", 
    RowBox[{"Times", ",", "ones", ",", "ones", ",", "xgrid"}], "]"}]}], 
  ";"}]}], "Input",
 CellChangeTimes->{{3.684061933737687*^9, 3.684062004743791*^9}, {
  3.751373005442266*^9, 3.751373006458858*^9}},
 CellLabel->"In[18]:=",ExpressionUUID->"1c143c3e-c6c6-47a6-8c48-cf74e9a356b0"]
}, Open  ]],

Cell[CellGroupData[{

Cell["ground-state calculation", "Subsection",
 CellChangeTimes->{{3.684059761727551*^9, 
  3.684059764648835*^9}},ExpressionUUID->"8043e2ed-65d9-4434-aa2d-\
af823e57ef0e"],

Cell[TextData[{
 "normalize a wavefunction with coefficients ",
 Cell[BoxData[
  FormBox["v", TraditionalForm]],ExpressionUUID->
  "5a9ff426-2c7d-4b29-bc7d-e0c8a183703e"],
 ":"
}], "Text",
 CellChangeTimes->{{3.684057256288631*^9, 3.684057287304566*^9}, {
  3.7336586669466248`*^9, 
  3.7336586703669147`*^9}},ExpressionUUID->"b621deea-5c67-4baa-825b-\
e70ed9c02d3e"],

Cell[BoxData[
 RowBox[{
  RowBox[{"nn", "[", "v_", "]"}], ":=", 
  FractionBox["v", 
   RowBox[{"Norm", "[", 
    RowBox[{"Flatten", "[", "v", "]"}], "]"}]]}]], "Input",
 CellChangeTimes->{{3.684057270040348*^9, 3.684057284267168*^9}, {
  3.7336586625269127`*^9, 3.733658673000225*^9}},
 CellLabel->"In[22]:=",ExpressionUUID->"8c24961a-b5e3-470d-a71b-78365c395bd3"],

Cell[TextData[{
 "calculate the ground-state wavefunction and its total energy and chemical \
potential, using a time step ",
 Cell[BoxData[
  FormBox["\[Delta]\[Beta]", TraditionalForm]],ExpressionUUID->
  "78538734-c22e-4b03-938e-c6f478218b50"],
 ":"
}], "Text",
 CellChangeTimes->{{3.684057294707698*^9, 
  3.684057324158886*^9}},ExpressionUUID->"72c33e1a-fd7b-4527-a133-\
024fd9028a2b"],

Cell[BoxData[
 RowBox[{
  RowBox[{"groundstate", "[", 
   RowBox[{
    RowBox[{"n_", "?", "NumericQ"}], ",", 
    RowBox[{"\[Delta]\[Beta]_", "?", "NumericQ"}], ",", 
    RowBox[{"tolerance_:", 
     SuperscriptBox["10", 
      RowBox[{"-", "6"}]]}]}], "]"}], ":=", 
  RowBox[{"Module", "[", 
   RowBox[{
    RowBox[{"{", 
     RowBox[{
     "Kn", ",", "Ke", ",", "propKin", ",", "propPot2", ",", "prop", ",", "v0",
       ",", "\[Gamma]", ",", "Ekin", ",", "Epot", ",", "Eint", ",", "Etot", 
      ",", "\[Mu]"}], "}"}], ",", 
    RowBox[{"(*", " ", 
     RowBox[{
     "compute", " ", "the", " ", "diagonal", " ", "elements", " ", "of", " ", 
      
      RowBox[{"exp", "[", 
       RowBox[{
        RowBox[{"-", "\[Delta]\[Beta]"}], "*", "T"}], "]"}]}], " ", "*)"}], 
    "\[IndentingNewLine]", 
    RowBox[{
     RowBox[{"Kn", "=", 
      RowBox[{
       FractionBox[
        RowBox[{
         SuperscriptBox["\[Pi]", "2"], 
         SuperscriptBox["\[HBar]", "2"]}], 
        RowBox[{"2", "m", " ", 
         SuperscriptBox["a", "2"]}]], 
       RowBox[{"Table", "[", 
        RowBox[{
         RowBox[{
          SuperscriptBox["nx", "2"], "+", 
          SuperscriptBox["ny", "2"], "+", 
          SuperscriptBox["nz", "2"]}], ",", 
         RowBox[{"{", 
          RowBox[{"nx", ",", "nmax"}], "}"}], ",", 
         RowBox[{"{", 
          RowBox[{"ny", ",", "nmax"}], "}"}], ",", 
         RowBox[{"{", 
          RowBox[{"nz", ",", "nmax"}], "}"}]}], "]"}]}]}], ";", 
     "\[IndentingNewLine]", 
     RowBox[{"Ke", "=", 
      RowBox[{
       RowBox[{"Exp", "[", 
        RowBox[{
         RowBox[{"-", "\[Delta]\[Beta]"}], "*", "Kn"}], "]"}], "//", "N"}]}], 
     ";", "\[IndentingNewLine]", 
     RowBox[{"(*", " ", 
      RowBox[{
       RowBox[{
       "propagate", " ", "by", " ", "a", " ", "full", " ", "imaginary"}], "-",
        "time", "-", 
       RowBox[{"step", " ", "with", " ", "T"}]}], " ", "*)"}], 
     "\[IndentingNewLine]", 
     RowBox[{
      RowBox[{"propKin", "[", "v_", "]"}], ":=", 
      RowBox[{"nn", "[", 
       RowBox[{"FourierDST", "[", 
        RowBox[{
         RowBox[{"Ke", "*", 
          RowBox[{"FourierDST", "[", 
           RowBox[{"v", ",", "1"}], "]"}]}], ",", "1"}], "]"}], "]"}]}], ";", 
     "\[IndentingNewLine]", 
     RowBox[{"(*", " ", 
      RowBox[{
       RowBox[{
       "propagate", " ", "by", " ", "a", " ", "half", " ", "imaginary"}], "-",
        "time", "-", 
       RowBox[{"step", " ", "with", " ", "V"}]}], " ", "*)"}], 
     "\[IndentingNewLine]", 
     RowBox[{
      RowBox[{"propPot2", "[", "v_", "]"}], ":=", 
      RowBox[{"nn", "[", 
       RowBox[{
        RowBox[{"Exp", "[", 
         RowBox[{
          RowBox[{"-", 
           FractionBox["\[Delta]\[Beta]", "2"]}], 
          RowBox[{"(", 
           RowBox[{"Wgrid", "+", 
            RowBox[{"\[Kappa]", 
             RowBox[{"(", 
              RowBox[{"n", "-", "1"}], ")"}], 
             RowBox[{
              SuperscriptBox[
               RowBox[{"Abs", "[", "v", "]"}], "2"], "/", 
              SuperscriptBox["\[CapitalDelta]", "3"]}]}]}], ")"}]}], "]"}], 
        "*", "v"}], "]"}]}], ";", "\[IndentingNewLine]", 
     RowBox[{"(*", " ", 
      RowBox[{
       RowBox[{
       "propagate", " ", "by", " ", "a", " ", "full", " ", "imaginary"}], "-",
        "time", "-", 
       RowBox[{"step", " ", "by"}]}], " ", "*)"}], "\[IndentingNewLine]", 
     RowBox[{"(*", " ", 
      RowBox[{"H", "=", 
       RowBox[{"T", "+", 
        RowBox[{
        "V", " ", "using", " ", "the", " ", "Trotter", " ", 
         "approximation"}]}]}], " ", "*)"}], "\[IndentingNewLine]", 
     RowBox[{
      RowBox[{"prop", "[", "v_", "]"}], ":=", 
      RowBox[{"propPot2", "[", 
       RowBox[{"propKin", "[", 
        RowBox[{"propPot2", "[", "v", "]"}], "]"}], "]"}]}], ";", 
     "\[IndentingNewLine]", 
     RowBox[{"(*", " ", 
      RowBox[{"random", " ", "starting", " ", "point"}], " ", "*)"}], 
     "\[IndentingNewLine]", 
     RowBox[{"v0", "=", 
      RowBox[{"nn", "@", 
       RowBox[{"RandomVariate", "[", 
        RowBox[{
         RowBox[{"NormalDistribution", "[", "]"}], ",", 
         RowBox[{"{", 
          RowBox[{"nmax", ",", "nmax", ",", "nmax"}], "}"}]}], "]"}]}]}], ";",
      "\[IndentingNewLine]", 
     RowBox[{"(*", " ", 
      RowBox[{
      "propagation", " ", "to", " ", "the", " ", "ground", " ", "state"}], 
      " ", "*)"}], "\[IndentingNewLine]", 
     RowBox[{"\[Gamma]", "=", 
      RowBox[{"FixedPoint", "[", 
       RowBox[{"prop", ",", "v0", ",", 
        RowBox[{"SameTest", "\[Rule]", 
         RowBox[{"Function", "[", 
          RowBox[{
           RowBox[{"{", 
            RowBox[{"v1", ",", "v2"}], "}"}], ",", 
           RowBox[{
            RowBox[{"Norm", "[", 
             RowBox[{"Flatten", "[", 
              RowBox[{"v1", "-", "v2"}], "]"}], "]"}], "<", "tolerance"}]}], 
          "]"}]}]}], "]"}]}], ";", "\[IndentingNewLine]", 
     RowBox[{"(*", " ", 
      RowBox[{"energy", " ", "components"}], " ", "*)"}], 
     "\[IndentingNewLine]", 
     RowBox[{"Ekin", "=", 
      RowBox[{
       RowBox[{"Flatten", "[", "Kn", "]"}], ".", 
       RowBox[{"Flatten", "[", 
        SuperscriptBox[
         RowBox[{"Abs", "[", 
          RowBox[{"FourierDST", "[", 
           RowBox[{"\[Gamma]", ",", "1"}], "]"}], "]"}], "2"], "]"}]}]}], ";",
      " ", 
     RowBox[{"(*", " ", "kinetic", " ", "*)"}], "\[IndentingNewLine]", 
     RowBox[{"Epot", "=", 
      RowBox[{
       RowBox[{"Flatten", "[", "Wgrid", "]"}], ".", 
       RowBox[{"Flatten", "[", 
        SuperscriptBox[
         RowBox[{"Abs", "[", "\[Gamma]", "]"}], "2"], "]"}]}]}], ";", " ", 
     RowBox[{"(*", " ", "potential", " ", "*)"}], "\[IndentingNewLine]", 
     RowBox[{"Eint", "=", 
      RowBox[{
       FractionBox["\[Kappa]", "2"], 
       RowBox[{"(", 
        RowBox[{"n", "-", "1"}], ")"}], 
       RowBox[{
        RowBox[{"Total", "[", 
         RowBox[{"Flatten", "[", 
          SuperscriptBox[
           RowBox[{"Abs", "[", "\[Gamma]", "]"}], "4"], "]"}], "]"}], "/", 
        SuperscriptBox["\[CapitalDelta]", "3"]}]}]}], ";", " ", 
     RowBox[{"(*", " ", "interaction", " ", "*)"}], "\[IndentingNewLine]", 
     RowBox[{"(*", " ", 
      RowBox[{"total", " ", "energy"}], " ", "*)"}], "\[IndentingNewLine]", 
     RowBox[{"Etot", "=", 
      RowBox[{"Ekin", "+", "Epot", "+", "Eint"}]}], ";", 
     "\[IndentingNewLine]", 
     RowBox[{"(*", " ", 
      RowBox[{"chemical", " ", "potential"}], " ", "*)"}], 
     "\[IndentingNewLine]", 
     RowBox[{"\[Mu]", "=", 
      RowBox[{"Ekin", "+", "Epot", "+", 
       RowBox[{"2", "Eint"}]}]}], ";", "\[IndentingNewLine]", 
     RowBox[{"(*", " ", 
      RowBox[{
       RowBox[{"return", " ", "energy"}], ",", " ", 
       RowBox[{"chemical", " ", "potential"}], ",", " ", "coefficients"}], 
      " ", "*)"}], "\[IndentingNewLine]", 
     RowBox[{"{", 
      RowBox[{"Etot", ",", "\[Mu]", ",", "\[Gamma]"}], "}"}]}]}], 
   "]"}]}]], "Input",
 CellChangeTimes->{{3.68405733160135*^9, 3.684057383393914*^9}, {
   3.684057459929296*^9, 3.684057732644191*^9}, 3.684057773877116*^9, {
   3.6840597908483257`*^9, 3.684059861898757*^9}, {3.6840599554652567`*^9, 
   3.684060025169744*^9}, {3.6840602377639513`*^9, 3.684060459276626*^9}, {
   3.6840607765194807`*^9, 3.684060779162245*^9}, {3.6840609601233587`*^9, 
   3.6840609603713827`*^9}, 3.703418534955822*^9, {3.733658725997652*^9, 
   3.733658756637309*^9}, {3.737523794898797*^9, 3.7375237995994387`*^9}, {
   3.7375430638061237`*^9, 3.73754308596066*^9}, {3.751373007543604*^9, 
   3.751373008511248*^9}},
 CellLabel->"In[23]:=",ExpressionUUID->"54a5e15a-7dd6-4e41-a19d-59b2d5f09d0b"]
}, Open  ]],

Cell[CellGroupData[{

Cell["calculations", "Subsection",
 CellChangeTimes->{{3.684060528507798*^9, 
  3.684060529665615*^9}},ExpressionUUID->"07c71c33-3afa-4096-8f93-\
715b378da1e3"],

Cell[TextData[{
 "calculate the ground-state wavefunction for ",
 Cell[BoxData[
  FormBox[
   RowBox[{"N", "=", "1000"}], TraditionalForm]],ExpressionUUID->
  "e9ef50a1-747a-4aee-bd1a-4503ba21eb1b"],
 " atoms:"
}], "Text",
 CellChangeTimes->{{3.684060977125709*^9, 
  3.684060988421146*^9}},ExpressionUUID->"c1155f24-9dd6-4d11-a58c-\
c888afaabdf7"],

Cell[BoxData[{
 RowBox[{
  RowBox[{
   RowBox[{"{", 
    RowBox[{"Etot", ",", "\[Mu]", ",", "\[Gamma]"}], "}"}], "=", 
   RowBox[{"groundstate", "[", 
    RowBox[{"1000", ",", 
     SuperscriptBox["10", 
      RowBox[{"-", "5"}]]}], "]"}]}], ";"}], "\[IndentingNewLine]", 
 RowBox[{
  RowBox[{"{", 
   RowBox[{"Etot", ",", "\[Mu]"}], "}"}], "*", 
  RowBox[{"UnitConvert", "[", 
   RowBox[{"EnergyUnit", ",", "\"\<Joules\>\""}], "]"}]}]}], "Input",
 CellChangeTimes->{{3.684060557453809*^9, 3.684060568106621*^9}, {
  3.6840607988564377`*^9, 3.6840608279836473`*^9}, {3.6840609914485292`*^9, 
  3.684061002575706*^9}, {3.684062149553297*^9, 3.684062149732501*^9}, {
  3.684062428702181*^9, 3.684062434393403*^9}, {3.7375472986966667`*^9, 
  3.7375473326964197`*^9}, {3.737552025710217*^9, 3.7375520335556803`*^9}},
 CellLabel->"In[24]:=",ExpressionUUID->"6c0ad4a9-3fee-422a-803e-89c514b9faab"],

Cell["convert to Hertz via Planck\[CloseCurlyQuote]s constant:", "Text",
 CellChangeTimes->{{3.737553723398507*^9, 
  3.737553733312944*^9}},ExpressionUUID->"3831ed82-40be-4075-a997-\
f8a50b894aac"],

Cell[BoxData[
 RowBox[{
  RowBox[{"{", 
   RowBox[{"Etot", ",", "\[Mu]"}], "}"}], "*", 
  RowBox[{"UnitConvert", "[", 
   RowBox[{
    FractionBox["EnergyUnit", 
     RowBox[{"Quantity", "[", "\"\<PlanckConstant\>\"", "]"}]], ",", 
    "\"\<Hertz\>\""}], "]"}]}]], "Input",
 CellChangeTimes->{{3.7375535772695293`*^9, 3.737553586206682*^9}},
 CellLabel->"In[26]:=",ExpressionUUID->"7703176b-e3e2-4bbe-9a1c-f8adfa90c80b"],

Cell["\<\
show the 3D density as an iso-density contour at half the maximum density:\
\>", "Text",
 CellChangeTimes->{{3.684061638484479*^9, 3.684061658430255*^9}, {
  3.6840618212289886`*^9, 
  3.684061842380374*^9}},ExpressionUUID->"41c7d5a8-5ce4-4683-a06c-\
f452f2334d8a"],

Cell[BoxData[{
 RowBox[{
  RowBox[{"\[Rho]", "=", 
   RowBox[{
    SuperscriptBox[
     RowBox[{"Abs", "[", "\[Gamma]", "]"}], "2"], "/", 
    SuperscriptBox["\[CapitalDelta]", "3"]}]}], 
  ";"}], "\[IndentingNewLine]", 
 RowBox[{"ListContourPlot3D", "[", 
  RowBox[{"\[Rho]", ",", 
   RowBox[{"DataRange", "\[Rule]", 
    RowBox[{"a", 
     RowBox[{"(", 
      RowBox[{
       FractionBox["1", 
        RowBox[{"nmax", "+", "1"}]], "-", 
       FractionBox["1", "2"]}], ")"}], 
     RowBox[{"{", 
      RowBox[{
       RowBox[{"{", 
        RowBox[{
         RowBox[{"-", "1"}], ",", "1"}], "}"}], ",", 
       RowBox[{"{", 
        RowBox[{
         RowBox[{"-", "1"}], ",", "1"}], "}"}], ",", 
       RowBox[{"{", 
        RowBox[{
         RowBox[{"-", "1"}], ",", "1"}], "}"}]}], "}"}]}]}], ",", 
   RowBox[{"Contours", "\[Rule]", 
    RowBox[{"{", 
     RowBox[{
      FractionBox["1", "2"], 
      RowBox[{"Max", "[", "\[Rho]", "]"}]}], "}"}]}], ",", 
   RowBox[{"BoxRatios", "\[Rule]", "Automatic"}]}], "]"}]}], "Input",
 CellChangeTimes->{{3.684061661415805*^9, 3.6840617149489393`*^9}, {
  3.684061761110738*^9, 3.6840617949912453`*^9}, {3.68406185194423*^9, 
  3.684061901986848*^9}, {3.684062438837173*^9, 3.684062439129676*^9}, {
  3.729425776834818*^9, 3.729425779442204*^9}, {3.7375430947313423`*^9, 
  3.737543098541533*^9}},
 CellLabel->"In[27]:=",ExpressionUUID->"92fe91bb-272d-4e91-babf-63870917a188"],

Cell["calculate mean values of the position operators:", "Text",
 CellChangeTimes->{{3.6840620326637287`*^9, 
  3.684062042519392*^9}},ExpressionUUID->"52672ed0-49cc-48fa-9d83-\
5f9f638dafc8"],

Cell[BoxData[{
 RowBox[{
  RowBox[{"X", "=", 
   RowBox[{
    RowBox[{"Flatten", "[", 
     SuperscriptBox[
      RowBox[{"Abs", "[", "\[Gamma]", "]"}], "2"], "]"}], ".", 
    RowBox[{"Flatten", "[", "xc", "]"}]}]}], ";"}], "\[IndentingNewLine]", 
 RowBox[{
  RowBox[{"Y", "=", 
   RowBox[{
    RowBox[{"Flatten", "[", 
     SuperscriptBox[
      RowBox[{"Abs", "[", "\[Gamma]", "]"}], "2"], "]"}], ".", 
    RowBox[{"Flatten", "[", "yc", "]"}]}]}], ";"}], "\[IndentingNewLine]", 
 RowBox[{
  RowBox[{"Z", "=", 
   RowBox[{
    RowBox[{"Flatten", "[", 
     SuperscriptBox[
      RowBox[{"Abs", "[", "\[Gamma]", "]"}], "2"], "]"}], ".", 
    RowBox[{"Flatten", "[", "zc", "]"}]}]}], ";"}], "\[IndentingNewLine]", 
 RowBox[{
  RowBox[{"XX", "=", 
   RowBox[{
    RowBox[{"Flatten", "[", 
     SuperscriptBox[
      RowBox[{"Abs", "[", "\[Gamma]", "]"}], "2"], "]"}], ".", 
    RowBox[{"Flatten", "[", 
     SuperscriptBox["xc", "2"], "]"}]}]}], ";"}], "\[IndentingNewLine]", 
 RowBox[{
  RowBox[{"YY", "=", 
   RowBox[{
    RowBox[{"Flatten", "[", 
     SuperscriptBox[
      RowBox[{"Abs", "[", "\[Gamma]", "]"}], "2"], "]"}], ".", 
    RowBox[{"Flatten", "[", 
     SuperscriptBox["yc", "2"], "]"}]}]}], ";"}], "\[IndentingNewLine]", 
 RowBox[{
  RowBox[{"ZZ", "=", 
   RowBox[{
    RowBox[{"Flatten", "[", 
     SuperscriptBox[
      RowBox[{"Abs", "[", "\[Gamma]", "]"}], "2"], "]"}], ".", 
    RowBox[{"Flatten", "[", 
     SuperscriptBox["zc", "2"], "]"}]}]}], ";"}]}], "Input",
 CellChangeTimes->{{3.7366743211599827`*^9, 3.7366743570343733`*^9}},
 CellLabel->"In[29]:=",ExpressionUUID->"a8e83254-717f-41c1-aff2-b4985b569f79"],

Cell["\<\
calculate the root-mean-square sizes of the BEC in the three Cartesian \
directions:\
\>", "Text",
 CellChangeTimes->{{3.6840620963407087`*^9, 
  3.684062116265316*^9}},ExpressionUUID->"3e165564-098d-4abd-91aa-\
cd934c0fa25c"],

Cell[BoxData[
 RowBox[{
  RowBox[{"{", 
   RowBox[{
    SqrtBox[
     RowBox[{"XX", "-", 
      SuperscriptBox["X", "2"]}]], ",", 
    SqrtBox[
     RowBox[{"YY", "-", 
      SuperscriptBox["Y", "2"]}]], ",", 
    SqrtBox[
     RowBox[{"ZZ", "-", 
      SuperscriptBox["Z", "2"]}]]}], "}"}], "*", "LengthUnit"}]], "Input",
 CellChangeTimes->{{3.6840621168930273`*^9, 3.684062139743855*^9}, {
  3.737547365407896*^9, 3.737547367421821*^9}},
 CellLabel->"In[35]:=",ExpressionUUID->"222962cf-2875-4c70-ae75-5a260c65028d"]
}, Open  ]]
}, Open  ]]
},
WindowSize->{1280, 778},
WindowMargins->{{0, Automatic}, {Automatic, 0}},
FrontEndVersion->"11.3 for Mac OS X x86 (32-bit, 64-bit Kernel) (March 5, \
2018)",
StyleDefinitions->"Default.nb"
]
(* End of Notebook Content *)

(* Internal cache information *)
(*CellTagsOutline
CellTagsIndex->{}
*)
(*CellTagsIndex
CellTagsIndex->{}
*)
(*NotebookFileOutline
Notebook[{
Cell[558, 20, 638, 14, 58, "Text",ExpressionUUID->"a2d4efbc-27c9-41fd-8aa0-d238baf57812"],
Cell[1199, 36, 2633, 48, 46, "Text",ExpressionUUID->"ae1b4ddb-f5ec-4dba-802d-64ed6c821f18"],
Cell[3835, 86, 481, 10, 35, "Text",ExpressionUUID->"44446b33-d998-4d53-b4ef-2c245b94359e"],
Cell[CellGroupData[{
Cell[4341, 100, 374, 5, 98, "Title",ExpressionUUID->"b22fba6e-b634-4920-a35f-fbea8a7ee82d"],
Cell[CellGroupData[{
Cell[4740, 109, 152, 3, 54, "Subsection",ExpressionUUID->"5b40b627-07a8-4c50-8afd-7e1243a2c4e4"],
Cell[4895, 114, 208, 4, 35, "Text",ExpressionUUID->"7720af8b-52db-4ea6-b828-7009757ca7a9"],
Cell[5106, 120, 935, 24, 137, "Input",ExpressionUUID->"4c9b1889-e5b5-4da7-9b3d-25e836fbd2ea"],
Cell[6044, 146, 161, 3, 35, "Text",ExpressionUUID->"026f9324-bb7b-4b4d-b673-2692fcf11544"],
Cell[6208, 151, 373, 8, 51, "Input",ExpressionUUID->"366fc385-13d4-41f7-9699-bb04076279b6"]
}, Open  ]],
Cell[CellGroupData[{
Cell[6618, 164, 165, 3, 54, "Subsection",ExpressionUUID->"dec91d92-e1c9-4e07-add9-c889f4001151"],
Cell[6786, 169, 158, 3, 35, "Text",ExpressionUUID->"440f7527-ac18-4180-8626-961743152a32"],
Cell[6947, 174, 367, 9, 49, "Input",ExpressionUUID->"f36d9a86-0548-40aa-807d-f1983d8a9a17"],
Cell[7317, 185, 171, 3, 35, "Text",ExpressionUUID->"15a5199b-6353-4840-86c6-f04d25c49129"],
Cell[7491, 190, 350, 9, 51, "Input",ExpressionUUID->"1675341c-34a2-439d-9494-632eca5b9b69"],
Cell[7844, 201, 168, 3, 35, "Text",ExpressionUUID->"ec4273ad-8611-4671-a29f-ca5bb5800eeb"],
Cell[8015, 206, 780, 21, 73, "Input",ExpressionUUID->"50cfe317-8e41-4799-a20b-8c2f66a25a3f"],
Cell[8798, 229, 176, 3, 35, "Text",ExpressionUUID->"1e7ddf49-ccaa-4e52-8643-66c8ffae7ec9"],
Cell[8977, 234, 361, 9, 51, "Input",ExpressionUUID->"c4039ab4-7cfb-40b2-b242-bbfc31ee7c14"],
Cell[9341, 245, 601, 16, 35, "Text",ExpressionUUID->"820b83c9-4197-4cb2-beae-08f7944d7729"],
Cell[9945, 263, 365, 8, 50, "Input",ExpressionUUID->"0dd9b70a-a4f8-4e2f-899a-6a4edfc5c218"]
}, Open  ]],
Cell[CellGroupData[{
Cell[10347, 276, 158, 3, 54, "Subsection",ExpressionUUID->"38882b32-ffb2-4aa5-919d-2f996d7820e7"],
Cell[10508, 281, 491, 15, 39, "Text",ExpressionUUID->"b0d6fb20-bb17-437a-bca0-aa321f414a83"],
Cell[11002, 298, 794, 20, 107, "Input",ExpressionUUID->"d5e165f9-d0ea-431b-97e9-c0ea487b131a"],
Cell[11799, 320, 172, 3, 35, "Text",ExpressionUUID->"7b1e58aa-6e2a-4411-ae34-630fa08a2713"],
Cell[11974, 325, 748, 22, 47, "Input",ExpressionUUID->"bdba2384-b042-429f-9785-5fb895425dc6"],
Cell[12725, 349, 198, 3, 35, "Text",ExpressionUUID->"01f8f462-2e4a-4f24-800f-f60397a70103"],
Cell[12926, 354, 361, 8, 30, "Input",ExpressionUUID->"bff51171-fecc-4661-8533-694db4c77dd5"],
Cell[13290, 364, 163, 3, 35, "Text",ExpressionUUID->"9b9f12a4-abc3-4b0d-a022-4fd31dbe7e8e"],
Cell[13456, 369, 827, 22, 94, "Input",ExpressionUUID->"1c143c3e-c6c6-47a6-8c48-cf74e9a356b0"]
}, Open  ]],
Cell[CellGroupData[{
Cell[14320, 396, 172, 3, 54, "Subsection",ExpressionUUID->"8043e2ed-65d9-4434-aa2d-af823e57ef0e"],
Cell[14495, 401, 367, 10, 35, "Text",ExpressionUUID->"b621deea-5c67-4baa-825b-e70ed9c02d3e"],
Cell[14865, 413, 365, 8, 47, "Input",ExpressionUUID->"8c24961a-b5e3-470d-a71b-78365c395bd3"],
Cell[15233, 423, 390, 10, 35, "Text",ExpressionUUID->"72c33e1a-fd7b-4527-a133-024fd9028a2b"],
Cell[15626, 435, 7658, 199, 610, "Input",ExpressionUUID->"54a5e15a-7dd6-4e41-a19d-59b2d5f09d0b"]
}, Open  ]],
Cell[CellGroupData[{
Cell[23321, 639, 160, 3, 54, "Subsection",ExpressionUUID->"07c71c33-3afa-4096-8f93-715b378da1e3"],
Cell[23484, 644, 348, 10, 35, "Text",ExpressionUUID->"c1155f24-9dd6-4d11-a58c-c888afaabdf7"],
Cell[23835, 656, 892, 19, 54, "Input",ExpressionUUID->"6c0ad4a9-3fee-422a-803e-89c514b9faab"],
Cell[24730, 677, 198, 3, 35, "Text",ExpressionUUID->"3831ed82-40be-4075-a997-f8a50b894aac"],
Cell[24931, 682, 420, 10, 51, "Input",ExpressionUUID->"7703176b-e3e2-4bbe-9a1c-f8adfa90c80b"],
Cell[25354, 694, 275, 6, 35, "Text",ExpressionUUID->"41c7d5a8-5ce4-4683-a06c-f452f2334d8a"],
Cell[25632, 702, 1420, 39, 71, "Input",ExpressionUUID->"92fe91bb-272d-4e91-babf-63870917a188"],
Cell[27055, 743, 192, 3, 35, "Text",ExpressionUUID->"52672ed0-49cc-48fa-9d83-5f9f638dafc8"],
Cell[27250, 748, 1632, 47, 148, "Input",ExpressionUUID->"a8e83254-717f-41c1-aff2-b4985b569f79"],
Cell[28885, 797, 236, 6, 35, "Text",ExpressionUUID->"3e165564-098d-4abd-91aa-cd934c0fa25c"],
Cell[29124, 805, 518, 15, 43, "Input",ExpressionUUID->"222962cf-2875-4c70-ae75-5a260c65028d"]
}, Open  ]]
}, Open  ]]
}
]
*)

